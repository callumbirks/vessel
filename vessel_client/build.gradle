plugins {
    id "fabric-loom"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.google.devtools.ksp"
}

base.archivesName.set("${rootProject.name}_client")

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.21:${project.parchment_version}")
    }

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    include(implementation(project(path: ":vessel_common", configuration: "namedElements")))

    // Moshi
    transitiveInclude(implementation "com.squareup.moshi:moshi:${project.moshi_version}")
    transitiveInclude(implementation "dev.zacsweers.moshix:moshi-sealed-runtime:${project.moshi_sealed_version}")

    ksp "com.squareup.moshi:moshi-kotlin-codegen:${project.moshi_version}"
    ksp "dev.zacsweers.moshix:moshi-sealed-codegen:${project.moshi_sealed_version}"

    configurations.transitiveInclude.resolvedConfiguration.resolvedArtifacts.forEach {
        include(it.moduleVersion.id.toString())
    }
}

loom {
    accessWidenerPath = file("src/main/resources/vessel.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}
