plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "2.2.0"
    id "org.jetbrains.kotlin.plugin.serialization" version "2.2.0"
    id "com.google.devtools.ksp" version "2.2.0-2.0.2"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url "https://maven.parchmentmc.org" }
    // for MineDown:
    maven { url "https://repo.minebench.de" }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"vessel" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

    accessWidenerPath = file("src/main/resources/vessel.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.21:${project.parchment_version}")
    }
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    implementation "net.kyori:adventure-api:${project.adventure_api_version}"
    modImplementation "net.kyori:adventure-platform-fabric:${project.adventure_fabric_version}"

    // Moshi
    include(implementation "com.squareup.moshi:moshi:${project.moshi_version}")
    include(implementation "dev.zacsweers.moshix:moshi-sealed-runtime:${project.moshi_sealed_version}")

    include(implementation "de.themoep:minedown-adventure:${project.minedown_version}")

    ksp "com.squareup.moshi:moshi-kotlin-codegen:${project.moshi_version}"
    ksp "dev.zacsweers.moshix:moshi-sealed-codegen:${project.moshi_sealed_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 21
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}